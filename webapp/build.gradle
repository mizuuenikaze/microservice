/*
 * Standalone application with embedded jetty
 */

buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			url 'https://plugins.gradle.org/m2/'
		}
	}
}

apply plugin: 'distribution'
apply plugin: 'war'

archivesBaseName  = 'muk-microservices'
ext.fullRepoUrl = "$config.binaryRepository.url/$config.binaryRepository.name"

repositories {
	maven { url fullRepoUrl }
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integrationtest/java')
		}
		resources.srcDir file('src/integrationtest/resources')
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
	remoteArchive.extendsFrom runtime
}

dependencies {
	compile 'javax.validation:validation-api:2.0.0.Alpha2'
	compile group: 'org.springframework', name: 'spring-webmvc', version: springVersion
	compile 'org.springframework.security:spring-security-web:4.2.2.RELEASE'
	compile 'org.springframework.security:spring-security-config:4.2.2.RELEASE'
	compile 'org.apache.felix:org.apache.felix.framework:5.6.2'
	compile 'org.apache.camel:camel-spring-osgi:2.3.0'
	compile group: 'org.apache.camel', name: 'camel-spring-javaconfig', version: camelVersion
	compile group: 'org.apache.camel', name: 'camel-spring-security', version: camelVersion
	compile group: 'org.apache.camel', name: 'camel-test-spring', version: camelVersion
	compile group: 'org.apache.camel', name: 'camel-swagger-java', version: camelVersion
	compile 'org.apache.activemq:activemq-kahadb-store:5.14.4'
	compile 'org.slf4j:slf4j-api:1.7.25'
	compile 'org.slf4j:slf4j-log4j12:1.7.25'
	compile 'log4j:log4j:1.2.17'
	
	compile group: 'org.restlet.jee', name: 'org.restlet.ext.spring', version: restletVersion
	compile 'org.thymeleaf:thymeleaf:2.1.4.RELEASE'
	compile 'org.thymeleaf:thymeleaf-spring4:2.1.4.RELEASE'
	compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4:2.1.2.RELEASE'
	compile 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:1.3.0'
	compile 'org.eclipse.jetty:jetty-webapp:9.4.3.v20170317'
	compile 'org.eclipse.jetty:jetty-annotations:9.4.3.v20170317'
	providedRuntime 'javax.servlet:javax.servlet-api:3.1.0'
	
	testCompile 'commons-io:commons-io:2.5'
}

ext.mainClassName = 'com.muk.app.StandAloneEntry'

task appRun (type: JavaExec) {
	main = mainClassName
	classpath = sourceSets.main.runtimeClasspath
	jvmArgs = applicationDefaultJvmArgs
	args = ['-hp', config.properties.webapp.camel.camel.jetty.httpPort, '-x', 'y']
	debug = false
}

task appDebug (type: JavaExec) {
	main = mainClassName
	classpath = sourceSets.main.runtimeClasspath
	jvmArgs = applicationDefaultJvmArgs
	args = ['-hp', config.properties.webapp.camel.camel.jetty.httpPort, '-x', 'y']
	debug = true
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

war {
	enabled = project.hasProperty('warify')
}

task warex (type: Copy) {
	into "$buildDir/exploded"
	with war

	doLast {
		ant.move(file: "$buildDir/exploded/WEB-INF/classes/com/muk/app", toFile: "$buildDir/exploded/com/muk/app")
		ant.move(todir: "$buildDir/appLibs") {
			fileset(dir: "$buildDir/exploded/WEB-INF/lib") {
				patternset() {
					include(name: '*jetty*.jar')
				}
				patternset() {
					include(name: 'asm*.jar')
				}
				patternset() {
					include(name: 'javax.*annotation-api*.jar')
				}
			}
		}

		copy {
			from "$buildDir/exploded/WEB-INF/lib"
			include 'camel-core*.jar', 'camel-spring-2*.jar', 'camel-spring-javaconfig*.jar', 'slf4j*.jar', 'log4j*.jar'
			into "$buildDir/appLibs"
		}

		jar.manifest {
			attributes ('Implementation-Title': 'MUK micro services')
			from sharedManifest
		}
		jar.manifest.writeTo("$buildDir/exploded/META-INF/MANIFEST.MF")
	}
}

task microsvc (type: Zip, dependsOn: warex) {
	extension = 'jar'
	metadataCharset = 'UTF-8'
	destinationDir = file("$libsDir")
	from "$buildDir/exploded"
}

/* --------- Distribution custom tasks ------------------------ */
task createStartScripts (type: CreateStartScripts, dependsOn: microsvc) {
	outputDir = file("$buildDir/scripts")
	applicationName = 'microservice'
	mainClassName = project.ext.mainClassName
	defaultJvmOpts = applicationDefaultJvmArgs
	//This must be modified buy the ebuild or other package management tool or manually.
	classpath = fileTree(dir: "$buildDir").matching { include 'appLibs/*.jar', "libs/$project.archivesBaseName*.jar" }
}

distributions {
	microservice {
		baseName = 'microservice'
	}
}

CopySpec libChildSpec = project.copySpec();
libChildSpec.into("lib");
libChildSpec.from(["$buildDir/libs"]) {
	include "$project.archivesBaseName*.jar"
}

CopySpec binChildSpec = project.copySpec();
binChildSpec.into("bin");
binChildSpec.from(project.createStartScripts);
binChildSpec.setFileMode(0755);

CopySpec childSpec = project.copySpec();
childSpec.from(project.file("src/dist"));
childSpec.with(libChildSpec);
childSpec.with(binChildSpec);

distributions.microservice.getContents().with(childSpec);


distTar.enabled = false
distZip.enabled = false
microserviceDistZip.enabled = false

/* ----------- task depends ---------------*/
check.dependsOn integrationTest
assemble.dependsOn microsvc
integrationTest.mustRunAfter test
microsvc.mustRunAfter war
